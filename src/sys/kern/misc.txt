; PedroM - Operating System for Ti-89/Ti-92+/V200.
; Copyright (C) 2003 PpHd
; 
; This program is free software; you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation; either version 2 of the License, or
; (at your option) any later version.
; 
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
; 
; You should have received a copy of the GNU General Public License
; along with this program; if not, write to the Free Software
; Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

;******************************************************************
;***                                                            ***
;***            	Misc Functions (1)			***
;***                                                            ***
;******************************************************************

; void OSContrastInit(void)
OSContrastInit:
	moveq	#$0F,d1				; Max value on HW1
	cmpi.b	#1,HW_VERSION
	bls.s	\ok
		moveq	#$1F,d1			; Max value on HW2
\ok:	move.b	d1,CONTRAST_MAX_VALUE		; Save Max Contrast Value
	lsr.b	#1,d1
	move.b	d1,CONTRAST_VALUE		; Initial value of contrast is Max/2+1
	
; void OSContrastUp(void)
OSContrastUp:
	movem.l	d0-d1,-(a7)			; Some programs assume d0,d1 aren't destroy ! (Tbo68k for example !)
	move.b	CONTRAST_MAX_VALUE,d1
	move.b	CONTRAST_VALUE,d0
	and.b	d1,d0
	cmp.b	d1,d0
	beq.s	ContrastReturn
	addq.b	#1,d0
	bra.s	ContrastSet

;void OSContrastSet(void)
OSContrastSet:
	movem.l	d0-d1,-(a7)			; Some programs assume d0,d1 aren't destroy ! (Tbo68k for example !)
	move.b	CONTRAST_VALUE,d0
	bra.s	ContrastSet

;void OSContrastDn(void)
OSContrastDn:
	movem.l	d0-d1,-(a7)			; Some programs assume d0,d1 aren't destroy ! (Tbo68k for example !)
	move.b	CONTRAST_MAX_VALUE,d1	; =$0F sur HW1
	move.b	CONTRAST_VALUE,d0
	and.b	d1,d0
	beq.s	ContrastReturn
	subq.b	#1,d0

ContrastSet:
	move.b	d0,CONTRAST_VALUE
	ifnd	TI89
		not.b	d0		; For 92+/V200
		cmpi.b	#1,HW_VERSION
		bne.s	Contrastno_hw1
			btst.l	#0,d0
			beq.s	Contrastclearit
				bset.b	#5,$600000
Contrastcont:		lsr.b	#1,d0
Contrastno_hw1	move.b	d0,$60001D
ContrastReturn:
	movem.l	(a7)+,d0-d1			; Some programs assume d0,d1 aren't destroy ! (Tbo68k for example !)
	rts
Contrastclearit:	bclr.b	#5,$600000
			bra.s	Contrastcont
	endif
	ifd	TI89
		ori.b	#$80,d0		; For 89
		cmpi.b	#1,HW_VERSION
		bne.s	Contrastno_hw1
			andi.b	#$EF,d0
Contrastno_hw1	move.b	d0,$60001D
ContrastReturn:
	movem.l	(a7)+,d0-d1			; Some programs assume d0,d1 aren't destroy ! (Tbo68k for example !)
	rts
	endif

OSInitBetweenKeyDelay:
	move.w	4(a7),KEY_ORG_REPEAT_CPT
	rts

OSInitKeyInitDelay:
	move.w	4(a7),KEY_ORG_START_CPT
	rts
